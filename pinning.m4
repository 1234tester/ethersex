dnl
dnl pinning.m4
dnl
dnl   Copyright (c) 2008 by Christian Dietrich <stettberger@dokucode.de>
dnl  
dnl   This program is free software; you can redistribute it and/or modify
dnl   it under the terms of the GNU General Public License version 2 as
dnl   published by the Free Software Foundation.
dnl  
dnl   This program is distributed in the hope that it will be useful,
dnl   but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl   GNU General Public License for more details.
dnl  
dnl   You should have received a copy of the GNU General Public License
dnl   along with this program; if not, write to the Free Software
dnl   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
dnl  
dnl   For more information on the GPL, please go to:
dnl   http://www.gnu.org/copyleft/gpl.html
dnl
/* This file has been generated by pinning.m4 automatically.
   Please do not modify it, edit the m4 script instead. */

divert(-1)dnl
changecom(`//')
define(`pin', `#define translit(`$1',`a-z', `A-Z')_PORT translit(substr(`$2', 1, 1), `a-z', `A-Z')
#define translit(`$1',`a-z', `A-Z')_PIN substr(`$2', 2, 1)
')
divert(1)
`#define _PORT_CHAR(character) PORT ## character
#define PORT_CHAR(character) _PORT_CHAR(character)

#define _PIN_CHAR(character) PIN ## character
#define PIN_CHAR(character) _PIN_CHAR(character)
 
#define _DDR_CHAR(character) DDR ## character
#define DDR_CHAR(character) _DDR_CHAR(character)


#define DDR_CONFIG_IN(pin)  DDR_CHAR( pin ## _PORT) &= ~_BV((pin ## _PIN))
#define DDR_CONFIG_OUT(pin)  DDR_CHAR( pin ## _PORT) |= _BV((pin ## _PIN))

#define PIN_BV(pin) (_BV(pin ## _PIN))
#define PIN_NR(pin) (pin ## _PIN)
#define PIN_HIGH(pin) (PIN_CHAR(pin ## _PORT) & _BV(pin ## _PIN))
#define PIN_SET(pin) PORT_CHAR(pin ## _PORT) |= _BV(pin ## _PIN)
#define PIN_CLEAR(pin) PORT_CHAR(pin ## _PORT) &= ~_BV(pin ## _PIN)
#define PIN_TOGGLE(pin) PORT_CHAR(pin ## _PORT) ^= _BV(pin ## _PIN)
#define PIN_PULSE(pin) do { PORT_CHAR(pin ## _PORT) &= ~_BV(pin ## _PIN); \
                              PORT_CHAR(pin ## _PORT) ^= _BV(pin ## _PIN); } while(0)'

divert(0)dnl
dnl
/* spi defines */
pin(SPI_MOSI, PB5)
pin(SPI_MISO, PB6)
pin(SPI_SCK, PB7)

/* port the enc28j60 is attached to
 * ATTENTION: EITHER USE SS OR MAKE SURE, SS IS PULLED HIGH OR AN OUTPUT! */

pin(SPI_CS_NET, PB4)

/* port the dataflash CS is attached to */
pin(SPI_CS_DF, PB1)

/* port the rfm12 module CS is attached to */
pin(SPI_CS_RFM12, PC3)

/* ps/2 pins */
pin(PS2_DATA, PA7)
pin(PS2_CLOCK, PA6)

/* pins for the hc595 shift register */
pin(HC595_DATA, PB6)
pin(HC595_CLOCK, PB2)
pin(HC595_STORE, PB7)

/* pins for the hc165 shift register */
pin(HC165_DATA, PB0)
pin(HC165_CLOCK, PB2)
pin(HC165_LOAD, PB1)

/* enc28j60 int line */
pin(INT_PIN, PB3)
/* enc28j60 wol line */
pin(WOL_PIN, PB2)

/* onewire support */
pin(ONEWIRE, PD6)

/* rc5 support */
pin(RC5_SEND, PD4)

/* fs20 support */
pin(FS20_SEND, PB2)
/* DO NOT CHANGE PIN!  USES INTERNAL COMPARATOR! */
pin(FS20_RECV, PB3)
